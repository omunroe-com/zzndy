%namespace UITestDsl
%using UITestDsl.Actions;
%partial

%union {
    public string identifier;
    public int number;
}

%token CLICK, TOOLBAR, APPEARS, NAMED, DUBBED, SWITCH
%token SELECT, IN, CLOSE, TO, FORM, ACTION, RUN, WAIT
%token <identifier> STRING, ID, PATH
%token <number> NUM

%%

File:
    RunExpr Rules | Rules;
    
Rules:
    Expr | Rules Expr;    

RunExpr:
    RUN PATH { RunApplication( $2 ); };

Expr:
    ClickAction | SwitchAction | SelectAction | NewFormExpectation | WaitAction | CloseAction;

NewFormExpectation:
    FORM APPEARS NAMED STRING DUBBED ID { AddNewFormExpectation( $4, $6 ); };

ClickAction:
    CLICK ClickTarget;

ClickTarget:
    ToolbarButton | Alias;
    
ToolbarButton:    
    TOOLBAR ACTION STRING { AddClickToolbarAction( $3 ); };
    
Alias:
    CLOSE { AddClickSpecialAction( ESpecialAction.Close ); };

SelectAction:
    SELECT STRING IN STRING { AddSelectAction( $2, $4 ); };

SwitchAction:
    SWITCH TO ID { AddSwitchAction( $3 ); };
    
WaitAction:
    WAIT NUM { AddWaitAction( $2 ); };    
    
CloseAction:
    CLOSE ID { AddCloseAction( $2 ); };
